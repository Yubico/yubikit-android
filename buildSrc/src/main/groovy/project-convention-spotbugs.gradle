import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort

plugins {
    id 'com.github.spotbugs'
}

dependencies {
    spotbugs 'com.github.spotbugs:spotbugs:4.8.6'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.14.0'

    implementation 'com.github.spotbugs:spotbugs-annotations:4.8.6'
}

spotbugs {
    ignoreFailures = true
    showStackTraces = false
    showProgress = false

    effort = Effort.MORE
    reportLevel = Confidence.valueOf('DEFAULT')
    excludeFilter = file("../spotbugs/excludeFilter.xml")
}

tasks.matching {
    it.name == "spotbugsTest"
}.configureEach {
    enabled = false
}

tasks.matching {
    it.name == "spotbugsMain" || it.name == "spotbugsRelease"
}.configureEach {
    enabled = true
    reports.create("html") {
        outputLocation = file("${project.rootDir}/build/spotbugs-html/spotbugs-${project.name}.html")
    }
    reports.create("sarif") {
        outputLocation = file("${project.rootDir}/build/spotbugs/spotbugs-${project.name}.sarif")
    }
}
