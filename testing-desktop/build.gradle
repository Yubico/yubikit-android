plugins {
    id 'java'
    id 'project-convention-spotless'
}

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'org.jetbrains:annotations:26.0.2-1'

    implementation project(':desktop')
    implementation project(':piv')
    implementation project(':testing')

    integrationTestImplementation 'junit:junit:4.13.2'
    integrationTestImplementation 'ch.qos.logback:logback-classic:1.5.19'

    implementation 'org.bouncycastle:bcpkix-jdk15to18:1.82'

    integrationTestRuntimeOnly('org.junit.vintage:junit-vintage-engine:6.0.0')
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging.showStandardStreams = true
}

description = "This module contains instrumented test framework and tests for yubikit-desktop."
