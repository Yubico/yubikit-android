// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin_version = '1.9.10'
    }
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'maven-publish'
    id 'com.github.spotbugs' version '5.0.14'
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).configureEach {
            options.compilerArgs.addAll(['-Xlint:deprecation', '-Xlint:unchecked'])
        }
    }

    group = 'com.yubico.yubikit'
}

subprojects {
    version = '2.4.1-SNAPSHOT'
    ext.pomName = "Yubico YubiKit ${project.name.capitalize()}"


    tasks.withType(Javadoc).tap {
        configureEach {
            options.addStringOption('Xdoclint:all,-missing', '-quiet')
        }
    }

    //noinspection UnnecessaryQualifiedReference
    tasks.withType(com.github.spotbugs.snom.SpotBugsTask).tap {
        configureEach {
            if (it.name == 'spotbugsTest' || it.name == 'spotbugsDebug') {
                enabled = false
            } else {
                group 'verification'
                reports {
                    xml.enabled = true
                    html.enabled = true
                }
            }
        }
    }
}
