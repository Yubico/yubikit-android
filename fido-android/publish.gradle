/*
 * Copyright (C) 2025 Yubico.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
android {

    publishing {
        singleVariant("release") {
        }
    }

    libraryVariants.configureEach { variant ->
        if (variant.name == 'release') {

            // build a jar with javadoc
            tasks.register('javadocJar', Jar) {
                dependsOn dokkaJavadoc
                from(dokkaJavadoc)
                archiveClassifier.set('javadoc')
            }

            // build a jar with source
            tasks.register('sourcesJar', Jar) {
                archiveClassifier.set('sources')
                from android.sourceSets.main.java.srcDirs
            }

            artifacts {
                archives javadocJar
                archives sourcesJar
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release
                artifact javadocJar
                artifact sourcesJar

                groupId = rootProject.group
                artifactId = project.name
                version = project.version

                apply from: rootProject.file('pom.gradle')
                pom(pomData)
            }
        }
        repositories {
            maven {
                name 'sonatype'
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username = findProperty("sonatype.username")
                    password = findProperty("sonatype.password")
                }
            }
        }
    }
    tasks.withType(Sign).configureEach {
        onlyIf { isReleaseVersion && System.getenv("NO_GPG_SIGN") != "true" }
    }
// TODO review
//    signing {
//        useGpgCmd()
//        sign publishing.publications.maven
//    }
}
